Array {
	values;

	`todo: Decide whether this should be an intrinsic or implemented more as it currently is. This implementation doesn't provide the caller with free `it` and `at` declarations that for-loops get, so that's the biggest motivator to refactor this. :refactor
	each { iteration;
		for values
			iteration(it)
		end
	}

	push { item; #intrinsic }
	pop {; #intrinsic }
	shift {; #intrinsic }
	unshift { item; #intrinsic }
	length {; #intrinsic }
	first { count; #intrinsic }
	last { count; #intrinsic }
	slice { from, to; #intrinsic }
	reverse {; #intrinsic }
	join { separator; #intrinsic }
	concat { other; #intrinsic }
	flatten {; #intrinsic }
	sort {; #intrinsic }
	uniq {; #intrinsic }
	include? { item; #intrinsic }
	empty? {; #intrinsic }

	find { func;
		for values
			if func(it)
				return it
			end
		end
		nil
	}

	any? { func;
		for values
			if func(it)
				return true
			end
		end
		false
	}

	all? { func;
		for values
			if !func(it)
				return false
			end
		end
		true
	}

	map { func;
		result = []
		for values
			result << func(it)
		end
		result
	}

	filter { func;
		result = []
		for values
			if func(it)
				result << it
			end
		end
		result
	}

	reduce { init, func;
		accumulator = init
		for values
			accumulator = func(accumulator, it)
		end
		accumulator
	}
}
