@use 'ore/html.ore'
@use 'ore/server.ore'

Page_Body | Body {
  css_display = 'flex'
  css_justify_content = 'center'
  css_align_items = 'center'
  css_min_height = '100vh'
  css_margin = '0'
}

```Layout component```
Layout | Html {
	title;
	body_content;

	css_filter = 'invert()'
	css_font_family = 'monospace'
	css_font_size = '1.2em'

	new { title, body_content ->
		.title = title
		.body_content = body_content
	}

	render { ->
		[
			Head(Title(title)),
			Page_Body(body_content)
		]
	}
}

Hero | Section {
	render { ->
		[
			H1("Ore"),
			P("Programming Language For Web Development")
		]
	}
}

Code_Example | Pre {
	subject;

	css_margin_left = '2em'
	css_background_color = '#ccc'
	css_border = '1px solid #888'
	css_padding = '1em'
	css_border_radius = '9px'
	css_display = 'inline-block'
}

```Feature section component```
Feature_Section | Dom {
	heading;
	items;

	new { heading, items ->
		.heading = heading
		.items = items
	}

	render { ->
		Div([
			H2(heading),
			Ul(items)
		])
	}
}

```Individual feature item```
Feature_Item | Dom {
	text;

	new { text ->
		.text = text
	}

	render { ->
		Li(text)
	}
}

```Main content container```
Page_Content | Div {
	snippet;

	new { snippet ->
		.snippet = snippet
	}

	render { ->
		naming = Feature_Section(
			"Naming conventions replace keywords",
			[
				Feature_Item("Capitalize classes"),
				Feature_Item("lowercase variables and functions"),
				Feature_Item("UPPERCASE constants")
			]
		)

		composition = Feature_Section(
			"Class composition replaces inheritance",
			[
				Feature_Item("| Union - merge classes (left side wins conflicts)"),
				Feature_Item("& Intersection - keep only shared declarations"),
				Feature_Item("~ Difference - discard right side declarations"),
				Feature_Item("^ Symmetric Difference - discard shared declarations")
			]
		)

		dot_notation = Feature_Section(
			"Dot notation for convenience",
			[
				Feature_Item("array.1.0.8 accesses nested structures"),
				Feature_Item(".identifier accesses instance scope"),
				Feature_Item("./identifier accesses static scope"),
				Feature_Item("../identifier accesses global scope"),
			]
		)

		other_features = Feature_Section(
			"Other features",
			[
				Feature_Item("First-class functions and classes"),
				Feature_Item("Built-in data types - Array, Tuple, Dictionary, Range")
			]
		)

		web_features = Feature_Section(
			"Web server support with routing",
			[
				Feature_Item("Route definitions use method://path syntax"),
				Feature_Item("URL parameters via :param syntax"),
				Feature_Item("Query string access via request.query"),
				Feature_Item("Request/response objects automatically available"),
				Feature_Item("Non-blocking #serve_http directive"),
				Feature_Item("Graceful shutdown handling")
			]
		)

		[
			Div([
				Hero(),
				Code_Example(.snippet)
			]),
			Div([
				naming,
				composition,
				dot_notation,
				other_features,
				web_features
			])
		]
	}
}

readme_snippet = "Hello {
	subject;

	new { subject ->
		.subject = subject
	}

	output { ->
		'Hi, \|subject\|'
	}
}

Hello().output()        # Hi, !
Hello('World').output() # Hi, World!"

```The app serving this HTML```
App | Server {
	get:// { ->
		Layout("Ore - Programming Language", Page_Content(readme_snippet))
	}
}

app = App()
@start app
