@use 'ore/html.ore'
@use 'ore/database.ore'
@use 'ore/record.ore'
@use 'ore/server.ore'

```Page components```
Page_Layout | Html {
	css_filter = 'invert()'
	css_font_size = '1.6em'
	css_font_family = 'monospace'
	css_padding = '3.2em'

	render { ->
		[Head(Title("Tasks")), Body(children)]
	}
}

Single_Task | Div {
	task;

	new { task = 'New Task' ->
		.task = task
	}

	render { ->
		[A('/', 'Back to all'), P("|id|: |task[:text]|")]
	}
}

Task_Item | Li {
	id;
	text;

	new { task ->
		.text = task[:text]
		.id   = task[:id]
	}

	render { ->
		A("/|id|", "|id|: |text|")
	}
}

List_Of_Tasks | Ul {
	tasks;

	new { tasks = [] ->
		.tasks = tasks
	}

	render { ->
		tasks.map({ it, at ->
			Task_Item(it)
		})
	}
}

New_Task_Form | Form {
	html_action = '/new'
	html_method = 'POST'

	render { ->
		[
			Label('Add a new task:'),
			Br(),
			Input_Field(),
			Br(),
			Submit_Button()
		]
	}
}

Input_Field | Textarea {
	html_type = 'text'
	html_name = 'task'
	html_placeholder = 'Enter task text...'
}

Submit_Button | Button {
	html_type = 'submit'

	render { -> 'Add Task' }
}

```Database```
db = Sqlite('./temp/task_app.db') # Sqlite composes with Database

# db = @connect Sqlite(...) is also valid
@connect db  # => calls Database#create_connection!

Task | Record {
	./database = ../db # Must set the database in order for Task query methods to work
	table_name = 'tasks'
}

if db.table_exists?('tasks')
	db.delete_table('tasks')
	db.create_table('tasks', { id: 'primary_key', text: 'String' })
else
	db.create_table('tasks', { id: 'primary_key', text: 'String' })
end

Task.create({text: "Make Databases work"})
Task.create({text: "Allow reading and writing Records"})
Task.create({text: "Show Tasks in this app"})
Task.create({text: "Implement user authentication"})
Task.create({text: "Add dark mode toggle"})
Task.create({text: "Write comprehensive documentation"})
Task.create({text: "Optimize database queries"})
Task.create({text: "Add file upload feature"})
Task.create({text: "Create RESTful API endpoints"})
Task.create({text: "Build admin dashboard"})

```Server```
App | Server {
	port = 80

	get:// home { ->
		tasks = Task.all()
		Page_Layout([
			H2('My Tasks'),
			New_Task_Form(),
			Hr(),
			List_Of_Tasks(tasks)
		])
	}

	get://:id get_task { id ->
		task = Task.find(id.to_i())
		Page_Layout(Single_Task(task))
	}

	post://new { ->
		id = Task.create({text: request.body.task })
		# You can call response.redirect("/") to manually set the destination, or call the named route functions:
		# home()
		get_task(id)
	}

	# note: The following routes prevent 500 errors since we're handling all routes here, nothing is filtered.
	get://favicon.ico { ->
		response.status = 404
		''
	}

	get://apple-touch-icon-precomposed.png { ->
		response.status = 404
		''
	}
}

app = App()

@start app
