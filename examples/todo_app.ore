#use 'ore/html.ore'
#use 'ore/database.ore'
#use 'ore/record.ore'
#use 'ore/server.ore'

```Page components```
Page_Layout | Html {
	css_filter = 'invert()'
	css_font_size = '1.6em'
	css_font_family = 'monospace'
	css_padding = '3.2em'

	render {;
		[Head(Title("Todos")), Body(children)]
	}
}

Single_Todo | Div {
	todo;

	new { todo = 'New Todo';
		.todo = todo
	}

	render {;
		[A('/', 'Back to all'), P("|id|: |todo[:text]|")]
	}
}

Todo_Item | Li {
	id;
	data;

	new { todo;
		.data = todo[:text]
		.id = todo[:id]
	}

	render {;
		A("/|id|", "|id|: |data|")
	}
}

List_Of_Todos | Ul {
	todos;

	new { todos = [];
		.todos = todos
	}

	render {;
		todos.map({ it, at;
			Todo_Item(it)
		})
	}
}

New_Todo_Form | Form {
	html_action = '/new'
	html_method = 'POST'

	render {;
		[
			Label('Add a new todo:'),
			Br(),
			Input_Field(),
			Br(),
			Submit_Button()
		]
	}
}

Input_Field | Textarea {
	html_type = 'text'
	html_name = 'todo'
	html_placeholder = 'Enter todo text...'
}

Submit_Button | Button {
	html_type = 'submit'

	render {; 'Add Todo' }
}

```Database```
db = Sqlite('./temp/todo_app.db') `Sqlite composes with Database

`db = #connect Sqlite(...) is also valid
#connect db  `=> calls Database#create_connection!

Todo | Record {
	..database = ~/db `Must set the database in order for Todo query methods to work
	table_name = 'todos'
}

if db.table_exists?('todos')
	db.delete_table('todos')
	db.create_table('todos', { id: 'primary_key', text: 'String' })
else
	db.create_table('todos', { id: 'primary_key', text: 'String' })
end

Todo.create({text: "Make Databases work"})
Todo.create({text: "Allow reading and writing Records"})
Todo.create({text: "Show Todos in this app"})
Todo.create({text: "Implement user authentication"})
Todo.create({text: "Add dark mode toggle"})
Todo.create({text: "Write comprehensive documentation"})
Todo.create({text: "Optimize database queries"})
Todo.create({text: "Add file upload feature"})
Todo.create({text: "Create RESTful API endpoints"})
Todo.create({text: "Build admin dashboard"})

```Server```
App | Server {
	port = 80

	home = get:// {;
		todos = Todo.all()
		Page_Layout([
			H2('My Todos'),
			New_Todo_Form(),
			Hr(),
			List_Of_Todos(todos)
		])
	}

	get://:id { id;
		todo = Todo.find(id.to_i())
		Page_Layout(Single_Todo(todo))
	}

	post://new {;
		Todo.create({text: request.body.todo })
		response.redirect("/")
	}

	`note: The following routes prevent 500 errors since we're handling all routes here, nothing is filtered.
	get://favicon.ico {;
		response.status = 404
		''
	}

	get://apple-touch-icon-precomposed.png {;
		response.status = 404
		''
	}
}

app = App()

#start app
