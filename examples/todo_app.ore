#use 'ore/html.ore'
#use 'ore/database.ore'
#use 'ore/record.ore'
#use 'ore/server.ore'

Page_Layout | Html {
	css_filter = 'invert()'
	css_font_size = '1.6em'
	css_font_family = 'monospace'
	css_padding = '3.2em'

	render {;
		[Head(Title("Todos")), Body(children)]
	}
}

Single_Todo | Div {
	todo;

	new { todo = 'New Todo';
		./todo = todo
	}

	render {;
		[A('/', 'Back to all'), Br(), todo]
	}
}

Todo_Item | Li {
	id;
	text;

	new { text, id;
		./text = text
		./id = id
	}

	render {;
		A("/|id|", text)
	}
}

List_Of_Todos | Ul {
	todos;

	new { todos = [];
		./todos = todos
	}

	render {;
		todos.map({it, at;
			Todo_Item(it, at)
		})
	}
}

App | Server {
	port = 80

	todos = []

	new { todos;
		./todos = todos
	}

	get:// {;
		Page_Layout(List_Of_Todos(todos))
	}

	get://:id { id;
		Page_Layout(Single_Todo(todos[id.to_i()]))
	}
}


db = Sqlite('./test/fixtures/sqlite_test.db') `Sqlite composes with Database
#connect db  `=> calls Database#create_connection!

Todo | Record {
	../database = ~/db
	table_name = 'todos'
}

todos = [ `todo: Replace these with a fetch from the database
	"Make Databases work",
	"Allow reading and writing Records"
]

app = App(todos)
#start app
